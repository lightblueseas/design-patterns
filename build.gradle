description = "$rootProject.name"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradlePluginVersionsVersion"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:$licenseGradlePluginVersion"
        classpath "org.ajoberstar.grgit:grgit-gradle:$grgitGradlePluginVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessGradlePluginVersion"
    }
}

apply plugin: "org.ajoberstar.grgit"

apply from: "gradle/grgit.gradle"

allprojects {
    group = "$groupPackage" as Object
    version = "$projectVersion" as Object
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "jacoco"
    apply plugin: "com.diffplug.spotless"
    // import gradle files
    apply from: "../gradle/dependencies.gradle"
    apply from: "../gradle/formatting.gradle"
    apply from: "../gradle/repositories.gradle"
    apply from: "../gradle/testing.gradle"

    sourceCompatibility = "$projectSourceCompatibility"

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption("html5", true)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        // uncomment the next line for build with compiler argument -Xlint:deprecation
        // options.compilerArgs << "-Xlint:deprecation"
        // uncomment the next line for build with compiler argument -Xlint:unchecked
        // options.compilerArgs << "-Xlint:unchecked"
    }

}
